(bwt.cfg (C) 2011, Dell Inc. Written by Matt Mahoney.
Licensed under GPL v3, http://www.gnu.org/licenses/gpl.html )

(BWT + order 0 model.
Requires 640 MB memory for files up to 128 MB - 256 bytes.
Use argument 1 to double, -1 to halve)
comp 1 0 $1+27 $1+27 1
  0 icm 5
hcomp
  halt
pcomp c:\res\zpaq\bwtrle c ;

  (read BWT, index into M, size in b)
  a> 255 ifnot
    *b=a b++

  (inverse BWT)
  else

    (index in last 4 bytes, put in c and R1)
    b-- a=*b
    b-- a<<= 8 a+=*b
    b-- a<<= 8 a+=*b
    b-- a<<= 8 a+=*b c=a r=a 1

    (save size in R2)
    a=b r=a 2

    (count bytes in H[~1..~255, ~0])
    do
      a=b a> 0 if
        b-- a=*b a++ a&= 255 d=a d! *d++
      forever
    endif

    (cumulative counts: H[~i=0..255] = count of bytes before i)
    d=0 d! *d= 1 a=0
    do
      a+=*d *d=a d--
    d<>a a! a> 255 a! d<>a until

    (build first part of linked list in H[0..idx-1])
    b=0 do
      a=c a>b if
        d=*b d! *d++ d=*d d-- *d=b
      b++ forever
    endif

    (rest of list in H[idx+1..n-1])
    b=c b++ c=r 2 do
      a=c a>b if
        d=*b d! *d++ d=*d d-- *d=b
      b++ forever
    endif

    (traverse list and output)
    d=r 1 do
      a=d a== 0 ifnot
        d=*d
        b=d a=*b out
      forever
    endif
  endif
  halt
end

