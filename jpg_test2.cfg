comp 17 17 12 16 33 (hh hm ph pm n)
  0 const 160
  1 cm $1+20 255
  2 cm $1+20 255
  3 icm $1+16
  4 cm $1+20 255
  5 icm $1+16
  6 cm $1+20 255
  7 cm $1+20 255
  8 cm $1+20 255
  9 icm $1+16
  10 cm $1+20 255
  11 icm $1+16
  12 cm $1+20 255
  13 icm $1+16
  14 cm $1+20 255
  15 icm $1+16
  16 cm $1+20 255
  17 icm $1+16
  18 cm $1+20 255
  19 icm $1+16
  20 cm $1+20 255
  21 icm $1+16
  22 icm $1+16
  23 cm 11 255

  24 mix 16 0 24 16 255
  25 mix 11 0 25 20 255
  26 mix2 0 24 25 40 0
  27 mix 0 0 26 32 0
  28 mix2 0 26 27 40 0
  29 sse 16 28 8 255
  30 mix2 8 28 29 40 255
  31 sse 8 30 8 255
  32 mix2 0 30 31 40 0
hcomp
  *c=a (save in rotating buffer)
  a=c a== 0 if a= 3 r=a 19 endif
  a=c a== 3 if
    (skip pcomp code)
    b=0 a=*b
    a== 0 ifnot b++ b++ a=*b a<<= 8 b-- a+=*b endif
    r=a 18
    a+= 8
    r=a 19
  endif

  a=r 19 a==c if b=c b-- b-- a=*b a> 192 if a< 255 endif if b-- a=*b a== 255 endif
    if b=c a=*b d=a b-- a=*b a<<= 8 a+=d d=a a=r 19 a+=d a++ a++ r=a 19 endif (skip comm/app field)
  endif
  a=r 19 a<c ifl
    a=c r=a 0
    a=r 25 a== 0 if
      b=c a=*b a< 5 if
        a*= 2 a+= 6 d=a b-- c=*b b-- a=*b a<<= 8 a+=c a==d if
          b-- a=*b a== 218 if
            b-- a=*b a== 255 if
              a=r 0 a-= 5 a+=d (d=r 18 a+=d) a++ a++ r=a 25 (find data start position)
            endif
          endif
        endif
      endif
    elsel
      a<c ifl
        a=*c
(main)
        b=c a=*b r=a 4 (r4=buf1)
        b-- a=*b r=a 5 (r5=buf2)
        (b-- a=*b r=a 6 r6=buf3)

        a=r 1 a== 0 if

          a=r 9 r=a 11 (r11=r9)
          a=r 8 r=a 9 (r9=r8)
          a=r 2 r=a 8 (r8=r10)

          a=*c r=a 8 (r10=RS)
          a=*c a>>= 4 r=a 2 (r2=S)
          a> 0 if
            a> 8 if a= 2 r=a 1 else a= 1 r=a 1 endif (r1=spos)
          endif
          a=*c a&= 15 r=a 3 (r3=R)

          b=0 a=r 7 a> 0 if d=r 2 a=r 3 a+=d a== 0 if r=a 7 b++ endif endif
          a=b a== 0 if a=r 7 a++ b=r 3 a+=b r=a 7 endif (r7=coef 0..63)

        else
          a-- r=a 1 (r1--)
        endif

  d=0 do *d=0 d++ a=d a< 24 while

  d=0
  d++
  a=r 7 hashd a=r 4 hashd a=r 1 hashd a=r 2 hashd a=r 3 hashd d++
  a=r 7 hashd a=r 4 hashd a=r 1 hashd a=r 2 hashd d++
  a=r 7 hashd a=r 4 hashd a=r 1 hashd a=r 3 hashd d++
  a=r 7 hashd a=r 4 hashd a=r 2 hashd a=r 3 hashd d++
  a=r 7 hashd a=r 4 hashd a=r 1 hashd d++
  a=r 7 hashd a=r 4 hashd a=r 2 hashd d++
  a=r 7 hashd a=r 4 hashd a=r 3 hashd d++
  a=r 7 hashd a=r 4 hashd a=r 5 hashd a=r 1 hashd d++
  a=r 7 hashd a=r 4 hashd a=r 5 hashd a=r 2 hashd d++
  a=r 7 hashd a=r 4 hashd a=r 5 hashd a=r 3 hashd d++


  a=r 4 hashd a=r 5 hashd a=r 1 hashd d++
  a=r 4 hashd a=r 5 hashd a=r 2 hashd d++
  a=r 4 hashd a=r 5 hashd a=r 3 hashd d++
  a=r 11 hashd a=r 9 hashd a=r 8 hashd a=r 10 hashd a=r 1 hashd d++
  a=r 11 hashd a=r 9 hashd a=r 8 hashd a=r 10 hashd a=r 1 hashd d++
  a=r 11 hashd a=r 9 hashd a=r 8 hashd a=r 10 hashd a=r 1 hashd d++
  a=r 9 hashd a=r 8 hashd a=r 10 hashd a=r 1 hashd d++
  a=r 9 hashd a=r 8 hashd a=r 10 hashd a=r 1 hashd d++
  a=r 9 hashd a=r 8 hashd a=r 10 hashd a=r 1 hashd d++
  a=r 8 hashd a=r 10 hashd a=r 1 hashd d++

  a=r 8 hashd a=r 10 hashd a=r 1 hashd d++
  a=r 8 hashd a=r 10 hashd a=r 1 hashd d++

   d++

  a=r 7 a*= 3 b=r 1 a+=b a<<= 9 *d=a (mix)
  d++
  a=r 1 a<<= 9 *d=a (mix)

  d++ d++ d++ d++

  a=r 7 a*= 3 b=r 1 a+=b a<<= 9 *d=a (sse)


      endif
    endif
    c=r 0
  endif

  c++
  halt
pcomp jpeg_jo e ;
 a> 255 ifnotl
   r=a 14 (save input byte to r14)
   a=c a== 0 if
     (start of file)
     a= 1 a<<= 16 r=a 2
     a= 7 r=a 6
     a= 1 a<<= 8 r=a 3
     a= 1 a<<= 11 r=a 4
     a= 206 a+=a r=a 5
   endif

(main)
 a=r 15 b=r 2
 a<b if a=r 7 else a= 1 endif
 a== 0 ifl
   b=r 15 a=r 14 *b=a a=b a++ r=a 15
   b=r 15 b-- a=*b out
   b=0 a=r 6 a> 0 if a-- r=a 6 a> 1 if b++ endif endif a=b
   a== 0 ifl
     b=r 15 b-- a=*b a< 5 if a*= 2 a+= 6 d=a b-- c=*b b-- a=*b a<<= 8 a+=c a==d if b-- a=*b a== 218 if b-- a=*b a== 255 if a=r 15 a-= 5 r=a 12 a+=d a++ a++ r=a 25 endif endif endif endif
     b=r 15 b-- a=*b c=a b-- a=*b a<<= 8 a+=c r=a 27
     b=r 15 b-- b-- b-- b-- a=*b a== 255 if
       b++ b=*b 
       a=r 6 a== 1 if a=b a> 192 if a< 255 if a=r 27 a+= 3 r=a 6 endif endif endif
       a=b a== 196 if a=r 16 a< 8 if d=r 16 a=r 15 *d=a d++ a=d r=a 16 endif endif
       a=b a== 192 if a=r 15 a-= 4 r=a 13 endif
       a=b a== 221 if a=r 15 a+= 2 r=a 24 endif
     endif
     a=r 24 b=r 15 a==b if a=r 27 r=a 8 endif
     a=r 25 
     a==b ifl
       a=0 r=a 18
       do
         d=r 18 a=*d r=a 20
         b=r 20 b-- c=*b b-- a=*b a<<= 8 a+=c a-= 2 c=r 20 a+=c r=a 17
         a=0 r=a 21
         do
           b=r 20 a=*b a>>= 4 r=a 22
           a=*b a&= 15 r=a 23
           a=r 20 a+= 17 r=a 24
           a=r 22 a*= 64 b=a a=r 23 a*= 16 a+=b r=a 25
           a=r 25 a*= 16 r=a 26
           a=0 r=a 19
           do
             a=r 19 b=r 26 a+=b c=r 5 a+=c d=a
             a=r 19 b=r 24 a+=b b=a a=*b b=a a>>= 4 c=a
             a=b a%= 16 a*= 16 a+=c *d=a
           a=r 19 a++ r=a 19 b=r 3 a<b while
           a=0 r=a 22 r=a 23 r=a 19
           do
             a= 28 b=r 25 a+=b b=r 19 a+=b d=a 
             a=r 23 a*= 2 r=a 23 *d=a 
             a=d a+= 128 d=a a=r 20 b=r 19 a+=b a++ b=a c=*b a=r 23 a+=c r=a 23 *d=a 
             a=d a+= 128 d=a a=r 22 b=r 26 a+=b *d=a 
             a=r 22 a+=c r=a 22
           a=r 19 a++ r=a 19 a< 16 while
           a=r 24 b=r 22 a+=b r=a 20
         c=0 a=r 20 b=r 17 a<b if c++ endif a=r 17 b=r 15 a<b if c++ endif a=r 20 b=r 4 a+=b b=a a=r 17 a<b if c++ endif a=r 21 a++ r=a 21 a< 10 if c++ endif a=c a== 4 while
       a=r 18 a++ r=a 18 b=r 16 a<b while
       a=r 12 a+= 4 b=a a=*b a< 5 if r=a 22 endif
       a=r 13 a+= 9 b=a a=*b a< 5 if r=a 23 endif
       a=0 r=a 7 r=a 20 r=a 18 
       do
         a=0 r=a 19
         do
           a=r 18 a*= 2 a+= 5 b=r 12 a+=b b=a d=a c=*b 
           a=r 19 a*= 3 a+= 10 b=r 13 a+=b b=a a=*b 
           a==c if
             b++ a=*b r=a 24 a>>= 4 c=a 
             b++ a=*b r=a 26
             b=d b++ a=*b r=a 25
             a=r 20 a<c if a=c r=a 20 endif
             a=r 24 a&= 15 a*=c r=a 24
             a=r 7 b=r 24 a+=b a> 10 if a=0 r=a 24 endif
             a=r 26 a> 3 if a=0 r=a 26 endif
             a=r 24
             a> 0 if
               do
                 a=r 25 a>>= 4 a&= 3 b=a 
                 a=r 7 a+= 8 d=a a+= 10 c=a *d=b 
                 a=r 25 a&= 3 d=c *d=a 
                 a=r 7 a++ r=a 7
               a=r 24 a-- r=a 24 a> 0 while
             endif
           endif
         a=r 19 a++ r=a 19 b=r 23 a<b while
       a=r 18 a++ r=a 18 b=r 22 a<b while
       a=r 7 a== 0 if a++ endif a*= 64 r=a 7
       a=0 r=a 6
     endif
   endif
 elsel
   a=r 6 
   a== 0 ifl
     a=r 10 a&= 63 r=a 23
     a=r 9 b=r 8 a==b if a=r 8 else a=0 endif a> 0 if a=r 23 else a++ endif
     a== 0 if
       a=r 1 a> 0 if
         a= 8 b=r 1 a-=b b=a a= 1 a<<=b b=a a=r 0 a++ a*=b a-- r=a 0
         out
         a== 255 if a=0 out endif
         a=0 r=a 1
         a=0 r=a 0
       endif
       a= 255 out 
       a=r 11 a+= 208 out
       a=r 11 a++ a%= 8 r=a 11
       a=0 r=a 9 r=a 10
     endif
     a=r 14 r=a 17
     a=r 17 a>>= 4 r=a 16
     a=0 r=a 15
     a=r 16 
     a> 8 if a= 1 r=a 6 else a> 0 if a= 2 r=a 6 else a= 3 r=a 6 endif endif
   else
     a< 3 if b=r 14 a=r 15 a<<= 8 a+=b r=a 15 a=r 6 a++ r=a 6 endif
   endif
   a=r 6 
   a== 3 ifl
     a=r 10 a&= 63 r=a 23
     a=r 10 a>>= 6 a+= 8 b=a a=r 23 a> 0 if a= 10 endif a+=b d=a b=*d 
     a=r 23 a> 0 if a= 4 endif a+=b r=a 24
     a=r 24 a*= 16 a+= 28 r=a 25
     a=0 r=a 18 r=a 22
     c= 255 
     do
       a=r 24 a<<= 8 b=r 5 a+=b b=c a+=b d=a a=*d b=r 17 
       a==b if a=c r=a 18 endif
     a=c c-- a> 0 while
     c=0 
     do
       a=r 25 a+=c d=a b=*d a=r 25 a+= 128 a+=c d=a a=*d a-=b b=a a=r 22 a+=b r=a 22
       c++ b=r 22 a=r 18 a<b if a= 17 else a=c endif
     a< 17 while
     c-- a=c r=a 20
     a=r 20 b=r 25 a+=b a+= 128 d=a a=*d b=r 22 a-=b b=a a=r 18 a+=b r=a 18


     a=r 20 a++ b=a a= 1 a<<=b b=r 0 a*=b c=a
     a=r 18 a+=c r=a 0
     a=r 20 a++ b=a a=r 1 a+=b r=a 1
     a=r 1 
     a> 7 if
       do
         a=r 1 a-= 8 r=a 1
         b=a a=r 0 a>>=b b=a out 
         a=b a== 255 if a=0 out endif
         a=b b=r 1 a<<=b b=a a=r 0 a-=b r=a 0
       a=r 1 a> 7 while
     endif

     b=r 16 a= 1 a<<=b b=r 0 a*=b c=a
     a=r 15 a+=c r=a 0
     b=r 16 a=r 1 a+=b r=a 1
     a=r 1 
     a> 7 if
       do
         a=r 1 a-= 8 r=a 1
         b=a a=r 0 a>>=b b=a out 
         a=b a== 255 if a=0 out endif
         a=b b=r 1 a<<=b b=a a=r 0 a-=b r=a 0
       a=r 1 a> 7 while
     endif



     a=r 17 a> 0 if a=0 else a=r 23 endif a> 0 if a= 64 b=r 23 a-=b else a=r 17 a%= 16 a++ endif b=a a=r 10 a+=b r=a 10
     b=r 7 a=r 10 a<b ifnot a=0 r=a 10 a=r 9 a++ r=a 9 endif
     a=0 r=a 6
   endif
 endif
(end of main)

 c= 1
 else

(end of file)
 a=r 1 
 a> 0 if
   a= 8 b=r 1 a-=b b=a a= 1 a<<=b b=a a=r 0 a++ a*=b a-- r=a 0
   out a=0 r=a 1
   a== 255 if a=0 out endif
   a=0 r=a 0
 endif
 a= 255 out 
 a= 217 out 

 c=0
 endif
 halt
end